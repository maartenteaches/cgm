----------------------------------------------------------------------------------------------------------------------------------------------------------------
      name:  <unnamed>
       log:  D:\Mijn documenten\projecten\track_mobility\cgm\ana\cgm_dta03.txt
  log type:  text
 opened on:  11 Mar 2025, 16:06:30

. 
. // clean spells
. // Who wrote it
. 
. version 18

. clear all

. macro drop _all

. 
. use cgm01
(raw educational spells from NEPS)

. 
. // sort order of spells 
. sort ID_t starty startm start, stable

. by ID_t : gen sort = _n

. 
. //----------------------------------------------------------------------------
. **#missing values
. // all missing start and all drop-out finish = only grundschule
. bys ID_t (sort) : replace start = 1 if sum(missing(start)) == _N & sum(finish==0)==_N
(57 real changes made)

. 
. // first start missing followed by finish gen sec to
. // first start enter gen sec 
. bys ID_t (sort) : replace start = 2 if start[1] == . & finish[1]== 11 & _n == 1
(335 real changes made)

. bys ID_t (sort) : replace start = 3 if start[1] == . & finish[1]== 12 & _n == 1
(227 real changes made)

. bys ID_t (sort) : replace start = 4 if start[1] == . & finish[1]== 14 & _n == 1
(89 real changes made)

. 
. // enter haupt --> miss --> enter real
. // enter haupt --> drop-out --> enter real
. bys ID_t (sort) : replace finish = 0 if start[2]== 2 & finish[2] == . & start[3]==3 & _n == 2
(0 real changes made)

. bys ID_t (sort) : replace finish = 0 if start[1]== 2 & finish[1] == . & start[2]==3 & _n == 1
(0 real changes made)

. 
. // enter real --> miss --> enter haupt
. // enter haupt --> drop-out --> enter haupt
. bys ID_t (sort) : replace finish = 0 if start[2]== 3 & finish[2] == . & start[3]==2 & _n == 2
(0 real changes made)

. bys ID_t (sort) : replace finish = 0 if start[1]== 3 & finish[1] == . & start[2]==2 & _n == 1
(0 real changes made)

. 
. // enter real --> miss --> enter gym
. // enter haupt --> drop-out --> enter gym
. bys ID_t (sort) : replace finish = 0 if start[2]== 3 & finish[2] == . & start[3]==4 & _n == 2
(0 real changes made)

. bys ID_t (sort) : replace finish = 0 if start[1]== 3 & finish[1] == . & start[2]==4 & _n == 1
(1 real change made)

. 
. // enter gym --> miss --> enter real
. // enter gym --> drop-out --> enter real
. bys ID_t (sort) : replace finish = 0 if start[2]== 4 & finish[2] == . & start[3]==3 & _n == 2
(0 real changes made)

. bys ID_t (sort) : replace finish = 0 if start[1]== 4 & finish[1] == . & start[2]==3 & _n == 1
(0 real changes made)

. 
. // drop spells where start and/or finish and/or starty is unknown
. drop if missing(start,finish, starty)
(5,717 observations deleted)

. 
. //----------------------------------------------------------------------------
. **# merge consecutive spells with the same start and end level 
. bys ID_t start finish (sort) : replace endm = endm[_N] if _N > 1 & sort[_N] - sort[1] == _N -1
(3,401 real changes made)

. bys ID_t start finish (sort) : replace endy = endy[_N] if _N > 1 & sort[_N] - sort[1] == _N -1
(4,830 real changes made)

. bys ID_t start finish (sort) : drop if _n > 1 & _N > 1 & sort[_N] - sort[1] == _N -1
(5,066 observations deleted)

. 
. // missing start but previous spell started with the level that finished in current spell
. bys ID_t (sort ) : replace sort = _n
(5,537 real changes made)

. bys ID_t (sort) : replace start = start[_n -1] ///
>       if start == . & ( start[_n-1] == 1 & finish == 10 ) | ///
>                       ( start[_n-1] == 2 & finish == 11 ) | ///
>                       ( start[_n-1] == 3 & finish == 12 ) | ///
>                       ( start[_n-1] == 4 & finish == 14 ) 
(433 real changes made)

. 
. // merge consecutive spells with same start and first ends are drop-outs
. bys ID_t (sort) : replace sort = _n
(0 real changes made)

. gen dropout = finish == 0

. bys ID_t start (sort) : replace dropout = sum(dropout)
(2,583 real changes made)

. bys ID_t start (sort) : replace dropout = dropout[_N-1] == _N-1
(7,973 real changes made)

. bys ID_t start (sort) : replace endm = endm[_N] if _N > 1 & sort[_N] - sort[1] == _N - 1 & dropout==1
(1,090 real changes made)

. bys ID_t start (sort) : replace endy = endy[_N] if _N > 1 & sort[_N] - sort[1] == _N - 1 & dropout==1
(1,486 real changes made)

. bys ID_t start (sort) : replace finish = finish[_N] if _N > 1 & sort[_N] - sort[1] == _N - 1 & dropout==1 & finish[_N] < .
(1,486 real changes made)

. bys ID_t start (sort) : drop if _n > 1 & _N > 1 & sort[_N] - sort[1] == _N-1 & dropout == 1
(1,494 observations deleted)

. drop dropout                                      

.         
. //---------------------------------------------------------------       
. **# duplicate spells and big gaps       
. // drop duplicate spells
. bys ID_t start finish endm endy startm starty : keep if _n == 1
(18 observations deleted)

. 
. // drop spells with a gap larger or equal to 10 years
. bys ID_t (sort) : gen diff = starty - endy[_n-1] 
(17,074 missing values generated)

. gen tobedropped = diff >= 10 & diff < .

. bys ID_t (sort) : replace tobedropped = sum(tobedropped)
(198 real changes made)

. drop if tobedropped
(1,160 observations deleted)

. drop tobedropped                                          

. 
. //-------------------------------------------------------------
. **# simplify/ correct some rare transitions                                       
. // no finishing (Fach)Abitur after entering Hauptschule
. // finish Realschule instead
. replace finish = 12 if start == 2 & inlist(finish, 13, 14)
(10 real changes made)

. 
. // no finishing Hauptschule after entering Gymnasium
. // finish Realschule instead
. replace finish = 12 if start == 4 & finish == 11
(31 real changes made)

. 
. // no finishing Hauptschule after entering vocaltional
. // finish Realschule instead
. replace finish = 12 if start == 7 & finish == 11
(88 real changes made)

. 
. // no finishing fachhochschule after entering vocational
. // finish vocational instead
. replace finish = 16 if start == 7 & finish == 17
(16 real changes made)

. 
. // no finishing vocational after entering fachhochschule
. // finish fachhochschule instead
. replace finish = 17 if start == 8 & finish == 16
(141 real changes made)

. 
. // no finishing fachhochschule after entering hochschule
. // finish hochschule instead
. replace finish = 18 if start == 9 & finish == 17
(67 real changes made)

. 
. // Fachabi = abi
. replace finish = 14 if finish == 13                                       
(1,166 real changes made)

. 
. // remove gesamtschule
. replace start = 2 if start == 5 & finish == 11
(251 real changes made)

. replace start = 3 if start == 5 & inlist(finish,0, 12)
(1,697 real changes made)

. replace start = 4 if start == 5 & inlist(finish,13, 14)
(529 real changes made)

. 
. // no double Hauptschule
. bys ID_t (sort) : drop if finish == 11 & finish[_n-1] == 11
(101 observations deleted)

. 
. // no double realschule
. bys ID_t (sort) : drop if finish == 12 & finish[_n-1] == 12
(386 observations deleted)

. 
. // no double Abi
. bys ID_t (sort) : drop if finish == 14 & finish[_n-1] == 14
(78 observations deleted)

. 
. // no double pre-voc
. bys ID_t (sort) : drop if finish == 15 & finish[_n-1] == 15
(12 observations deleted)

. 
. // no double voc
. bys ID_t (sort) : drop if finish == 16 & finish[_n-1] == 16
(279 observations deleted)

. 
. // no double Fachhochschule
. bys ID_t (sort) : drop if finish == 17 & finish[_n-1] == 17
(33 observations deleted)

. 
. // no double Hochschule
. bys ID_t (sort) : drop if finish == 18 & finish[_n-1] == 18
(45 observations deleted)

. 
. 
. // clean up sort
. bys ID_t (sort) : replace sort = _n
(1,681 real changes made)

. 
. compress
  variable sort was float now byte
  variable diff was float now byte
  (331,944 bytes saved)

. note: cgm03.dta \ cleaned spells \ cgm_dta03.do \ MLB TS 

. label data "cleaned spells"

. datasignature set, reset
  55324:23(65276):1421056373:3973176506       (data signature reset)

. save cgm03.dta, replace
file cgm03.dta saved

. 
. log close
      name:  <unnamed>
       log:  D:\Mijn documenten\projecten\track_mobility\cgm\ana\cgm_dta03.txt
  log type:  text
 closed on:  11 Mar 2025, 16:06:33
----------------------------------------------------------------------------------------------------------------------------------------------------------------
